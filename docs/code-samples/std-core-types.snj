module /std


public ------------------------------------------------------------------------


type () = __Unconstructible__


public ------------------------------------------------------------------------


type Bool =
	| False
	| True
	;


alias False = Bool.False
alias True = Bool.True


public ------------------------------------------------------------------------


trait #functor[A] {
	type Self[A]
	fun (Self[A]).map(transform: A -> B) -> Self[B]
}


trait #monad[A] {
	type Self[A]
	fun Self[A].wrap(item: A) -> Self[A]
	fun (Self[A]).and-then(operation: A -> Self[B]) -> Self[B]
}


trait #faillible[A, E] {
	type Self[A, E]
	extend Self[A, E] as #monad[A]
	fun (Self[A]) <fail> (make-error: E -> F) -> Result[A, F]
}


public ------------------------------------------------------------------------


type Option[A] =
	| Some(item: data A)
	| None
	;


alias Some[A] = Option[A].Some
alias None = Option[_].None


extend Option[A] as #functor[A]


fun (Option[A]).map(transform: A -> B) -> Option[B] =
	match self
		| Some(item) -> Some(transform(item))
		| None -> None
		;


extend Option[A] as #monad[A]


fun Option[A].wrap(item: A) -> Option[A] =
	Some(item)


fun (Option[A]).and-then(operation: A -> Option[B]) -> Option[B] =
	match self
		| Some(item) -> operation(item)
		| None -> None
		;


fun (Option[A]) as Result[A, None] =
	match self
		| Some(item) -> Ok(item)
		| None -> Err(None)
		;


fun (Result[A, None]) as Option[A] =
	match self
		| Ok(item) -> Some(item)
		| Err(None) -> None
		;


extend Option[A] as #unwrap[A, None]


fun (Option[A]).unwrapped -> Result[A, None] =
	match self
		| Some(item) -> Ok(item)
		| None -> Err(None)
		;


extend Option[A] as #else[A, ()]


fun (Option[A]) <else> (make-error: () -> E) -> Result[A, E] =
	match self
		| Some(item) -> Ok(item)
		| None -> Err(make-error())
		;


fun (Option[A]).ok-or(error: E) -> Result[A, E] =
	match self
		| Some(item) -> Ok(item)
		| None -> Err(error)
		;


forall N[B, F]
fun (Option[A]).shortcut(
	else-op: () -> N[B, F],
	then-op: A -> N[B, F],
) -> N[B, F] =
	match self
		| None -> else-op()
		| Some(item) -> then-op(item)
		;


public ------------------------------------------------------------------------


type Result[A, E #error] =
	| Ok(item: data A)
	| Err(error: data E)
	;


alias Ok[A] = Result[A, _].Ok
alias Err[E] = Result[_, E].Err


extend Result[A, E] as #functor[A]


fun (Result[A, E]).map(transform: A -> B) -> Result[B, E] =
	match self
		| Ok(item) -> Ok(transform(item))
		| Err(e) -> Err(e)
		;


extend Result[A, E] as #monad[A]


fun Result[A, E].ok(item: A) -> Result[A, E] =
	Ok(item)


fun (Result[A, E]).and-then(operation: A -> Result[B, E]) -> Result[B, E] =
	match self
		| Ok(item) -> operation(item)
		| Err(e) -> Err(e)
		;


extend Result[A, E] as #unwrap[A]


fun (Result[A, E]).unwrapped -> Result[A, E] =
	self


extend Result[A, E] as #else[A, E]


fun (Result[A, E]) <else> (transform: E -> F) -> Result[A, F] =
	match self
		| Ok(item) -> Ok(item)
		| Err(e) -> Err(transform(e))
		;
