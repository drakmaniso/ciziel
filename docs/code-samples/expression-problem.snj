<< The Expression Problem >>


module basic


def #eval =
	has (e: Self).eval -> Int


def Literal: #data =
	struct {
		value: Int,
	}


given T
def Addition[T]: #data =
	struct {
		left: Ref[T],
		right: Ref[T],
	}


given T: #eval
def #eval for Literal, Addition[T]


def (l: Literal).eval -> Int =
	l.value


given T: #eval
def (a: Addition[T]).eval -> Int =
	a.left.eval + a.right.eval


-------------------------------------------------------------------------------


module extended


use basic { #eval, Literal, Addition }


given T
def Negation[T]: #data =
	struct {
		subexpr: Ref[T],
	}


given T: #eval
def #eval for Negation[T]


given T: #eval
def (n: Negation[T]).eval -> Int =
	- n.subexpr.eval


def #pretty =
	has (p: Self).pretty -> String


given T: #pretty
def #pretty for Literal, Addition[T], Negation[T]


def (l: Literal).pretty -> String =
	l.value <to> String


given T: #pretty
def (a: Addition[T]).pretty -> String =
	let l = a.left.pretty
	let r = a.right.pretty
	in
	match (a.left, a.right)
	case (Literal(_), Literal(_)) then
		"{l} + {r}"
	case (Literal(_), _) then
		"{l} + ({r})"
	case (_, Literal(_)) then
		"({l}) + {r}"
	else (_, _) then
		"({l}) + ({r})"


given T: #pretty
def (n: Negation[T]).pretty -> String =
	"- ({n.subexpr.pretty})"


-------------------------------------------------------------------------------


module main


use std::io { &io }
use basic { #eval, Literal, Addition }
use extended { #pretty, Negation }


let Expr =
	| Literal
	| Addition[Expr]
	| Negation[Expr]


let Expr as #eval #pretty


def main() -> () &io =
	let e1: Expr = Addition(Literal(77), Negation(Literal(12)))
	do Io.print("{e1.pretty} = {e1.eval}")
	in ()
